// ==========================================================================
// Copyright 1995,2006,2008 Autodesk, Inc. All rights reserved.
//
// Use of this software is subject to the terms of the Autodesk
// license agreement provided at the time of installation or download,
// or which otherwise accompanies this software in either electronic
// or hard copy form.
// ==========================================================================

global proc AEptexColorTextureBrowser( string $fileAttribute )
{
	string  $workspace = `workspace -q -fn`;
	setWorkingDirectory $workspace "image" "sourceImages";
		
	string $ptxFilter = "*.ptx";
	string $result[] = `fileDialog2 -fileFilter $ptxFilter -fileMode 1 -dialogStyle 2 -caption "Open Ptex File"`;
		
	setAttr $fileAttribute -type "string" $result[0];

	string $currentDir = `workspace -q -dir`;
	retainWorkingDirectory ($currentDir);

	// Extract the name of the node from the node.attribute name
	//
	string $tokenArray[];
	tokenize($fileAttribute, ".", $tokenArray);

	string $fileNode = $tokenArray[0];
}

global proc AEptexColorTextureNameReplace (string $fileAttribute)
{
	connectControl -fileName textureNameField $fileAttribute;

	button -e -c ( "AEptexColorTextureBrowser \"" + $fileAttribute + "\"" ) browser;
}

global proc AEptexColorTextureNameNew (string $fileAttribute)
{
	setUITemplate -pst attributeEditorTemplate;
	rowLayout -nc 3 textureNameLayout;
		text -label "Ptex File";
		textField textureNameField;
		symbolButton -image "navButtonBrowse.png" browser;
	setParent ..;
	setUITemplate -ppt;
	
	AEptexColorTextureNameReplace $fileAttribute;
}

global proc AEptexColorTextureTemplate( string $nodeName )
{
	AEswatchDisplay $nodeName;
	editorTemplate -beginScrollLayout;

	editorTemplate -beginLayout "Ptex Texture Attributes" -collapse 0;
										
		editorTemplate -callCustom  "AEptexColorTextureNameNew" 
									"AEptexColorTextureNameReplace" 
									"fileName";
						
		editorTemplate -addControl "filterType";
		
		editorTemplate -addControl "filterSize";
				
	editorTemplate -endLayout;

	AEtexture2dCommon $nodeName;

	editorTemplate -addExtraControls;
	editorTemplate -endScrollLayout;
}

